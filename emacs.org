#+TITLE: Emacs Literate Configuration
#+AUTHOR: paschdan
#+PROPERTY: header-args :tangle yes

* Configuration
:PROPERTIES:
:VISIBILITY: children
:END:

** Table of Contents :TOC_4_gh:
- [[#configuration][Configuration]]
  - [[#about-this-file][About this file]]
  - [[#org-file-tweaks][Org File Tweaks]]
    - [[#automatically-tangle][Automatically Tangle]]
    - [[#visibility-settings][Visibility Settings]]
    - [[#table-of-contents][Table of Contents]]
  - [[#personal-information][Personal Information]]
  - [[#emacs-initialization][Emacs Initialization]]
    - [[#constants][Constants]]
      - [[#directories][Directories]]
    - [[#settings][Settings]]
    - [[#package-management][Package Management]]
      - [[#package-settings][Package Settings]]
      - [[#initialize-package-management][Initialize Package Management]]
  - [[#emacs-configuration][Emacs Configuration]]
    - [[#misc][Misc]]
      - [[#customel][custom.el]]
      - [[#autosaving][Autosaving]]
      - [[#recentf][recentf]]
    - [[#ui][UI]]
      - [[#defaults][Defaults]]
      - [[#theme][Theme]]
      - [[#which-key][which-key]]
    - [[#keybindings][Keybindings]]
      - [[#general][General]]
      - [[#truely-esc][Truely ESC]]
      - [[#evil][Evil]]
    - [[#ivy][Ivy]]
    - [[#counsel][Counsel]]
    - [[#swiper][Swiper]]
    - [[#magit][Magit]]
      - [[#evil-magit][Evil-Magit]]
    - [[#projectile][Projectile]]
      - [[#projectile-councel][projectile-councel]]
    - [[#languages][Languages]]
      - [[#org][Org]]
    - [[#toc-org][Toc-org]]
  - [[#post-initialization][Post Initialization]]

** About this file
This is an Emacs literate configuration template. It contains the basic structure
of a literate config along with some optimizations to ensure a fast load time.

** Org File Tweaks
There are a few tweaks included in this org file that make it a little easier to
work with.

*** Automatically Tangle
First there is a property defined on the file:

#+BEGIN_SRC :tangle no
header-args :tangle yes
#+END_SRC

This tells emacs to automatically tangle (include) all code blocks in this file when
generating the code for the config, unless the code block explicitly includes
=:tangle no= as the above code block does.

*** Visibility Settings
Next we have a property defined on the [[Configuration][Configuration]] heading that defines the visibility
that tells org to show it's direct children on startup. This way a clean outline of all
sub headings under Configuration is shown each time this file is opened in org-mode.

*** Table of Contents
Finally, there is a [[Table of Contents][Table of Contents]] heading that includes the tag: =:TOC_3_gh:=. This
tells an org-mode package =toc-org= to generate a table of contents under this heading
that has a max depth of 3 and is created using Github-style hrefs. This table of contents
is updated everytime the file is saved and makes for a functional table of contents that
works property directly on github.

** Personal Information
Let's set some variables with basic user information.

#+BEGIN_SRC emacs-lisp
(setq user-full-name "Daniel Paschke"
      user-mail-address "paschdan@gmail.com")
#+END_SRC

** Emacs Initialization
   
*** Constants
    
**** Directories
We want all generated directories in a .local subdir

#+BEGIN_SRC emacs-lisp
(setq +emacs-directory (file-truename user-emacs-directory))
(setq +local-directory (concat +emacs-directory ".local/"))
(setq +packages-directory (concat +local-directory "packages/"))
#+END_SRC

*** Settings
We're going to increase the gc-cons-threshold to a very high number to decrease the load and compile time.
We'll lower this value significantly after initialization has completed. We don't want to keep this value
too high or it will result in long GC pauses during normal usage.

#+BEGIN_SRC emacs-lisp
(eval-and-compile
  (setq gc-cons-threshold 402653184
        gc-cons-percentage 0.6))
#+END_SRC

Disable certain byte compiler warnings to cut down on the noise. This is a personal choice and can be removed
if you would like to see any and all byte compiler warnings.

#+BEGIN_SRC emacs-lisp
(setq byte-compile-warnings '(not free-vars unresolved noruntime lexical make-local))
#+END_SRC

*** Package Management

**** Package Settings
We're going to set the =load-path= ourselves and avoid calling =(package-initilize)= (for
performance reasons) so we need to set =package--init-file-ensured= to true to tell =package.el=
to not automatically call it on our behalf. Additionally we're setting
=package-enable-at-startup= to nil so that packages will not automatically be loaded for us since
=use-package= will be handling that.

#+BEGIN_SRC emacs-lisp
(eval-and-compile
  (setq load-prefer-newer t
        package-user-dir (expand-file-name "elpa" +packages-directory)
        package--init-file-ensured t
        package-enable-at-startup nil)

  (unless (file-directory-p package-user-dir)
    (make-directory package-user-dir t)))
#+END_SRC

**** Initialize Package Management
Next we are going to require =package.el= and add our additional package archives, 'melpa' and 'org'.
Afterwards we need to initialize our packages and then ensure that =use-package= is installed, which
we promptly install if it's missing. Finally we load =use-package= and tell it to always install any
missing packages.

Note that this entire block is wrapped in =eval-when-compile=. The effect of this is to perform all
of the package initialization during compilation so that when byte compiled, all of this time consuming
code is skipped. This can be done because the result of byte compiling =use-package= statements results
in the macro being fully expanded at which point =use-package= isn't actually required any longer.

Since the code is automatically compiled during runtime, if the configuration hasn't already been
previously compiled manually then all of the package initialization will still take place at startup.

#+BEGIN_SRC emacs-lisp
(eval-when-compile
  (require 'package)

  (unless (assoc-default "melpa" package-archives)
    (add-to-list 'package-archives '("melpa" . "http://melpa.org/packages/") t))
  (unless (assoc-default "org" package-archives)
    (add-to-list 'package-archives '("org" . "http://orgmode.org/elpa/") t))

  (package-initialize)
  (unless (package-installed-p 'use-package)
    (package-refresh-contents)
    (package-install 'use-package))
  (require 'use-package)
  (setq use-package-always-ensure t))
#+END_SRC

** Emacs Configuration

*** Misc
**** custom.el

#+BEGIN_SRC emacs-lisp
(setq custom-file (expand-file-name "custom.el" +local-directory))
(unless (file-exists-p custom-file)
  (write-region "" "" custom-file))
(load custom-file)
#+END_SRC

**** Autosaving
I want the auto-save-list directory in my local dir

#+BEGIN_SRC emacs-lisp
(setq auto-save-list-file-prefix (concat +local-directory "auto-save-list/.saves-"))
(setq auto-save-file-name-transforms `((".*" ,(concat +local-directory "auto-save-list/") t)))
#+END_SRC

**** recentf

#+BEGIN_SRC emacs-lisp
(use-package recentf
  :hook (after-init . recentf-mode)
  :init
  (setq recentf-save-file (concat +local-directory "recentf")
        recentf-max-saved-items 1000
        recentf-filename-handlers '(file-truename)
        recentf-auto-cleanup 'never)
  :config
  (add-to-list 'recentf-exclude "COMMIT_EDITMSG\\'"))
#+END_SRC
*** UI

**** Defaults

#+BEGIN_SRC emacs-lisp
(blink-cursor-mode -1)
(scroll-bar-mode -1)
(tool-bar-mode -1)
(tooltip-mode -1)
(setq mouse-highlight nil)
(setq column-number-mode t)
(setq-default cursor-in-non-selected-windows nil)
(setq x-underline-at-descent-line t)
(setq x-stretch-cursor t)
(setq frame-resize-pixelwise t)
(setq inhibit-startup-screen t)
#+END_SRC

**** Theme

#+BEGIN_SRC emacs-lisp
(use-package solarized-theme
  :init
  (setq solarized-use-variable-pitch nil)
  (setq solarized-scale-org-headlines nil)
  :config
  (load-theme 'solarized-dark t)
  (let ((line (face-attribute 'mode-line :underline)))
    (set-face-attribute 'mode-line          nil :overline   line)
    (set-face-attribute 'mode-line-inactive nil :overline   line)
    (set-face-attribute 'mode-line-inactive nil :underline  line)
    (set-face-attribute 'mode-line          nil :box        nil)
    (set-face-attribute 'mode-line-inactive nil :box        nil)
    (set-face-attribute 'mode-line-inactive nil :background "#002b36")))
#+END_SRC

**** which-key
 
#+BEGIN_SRC emacs-lisp
(use-package which-key
  :demand t
  :config
  (setq which-key-sort-order #'which-key-prefix-then-key-order
        which-key-sort-uppercase-first nil
        which-key-add-column-padding 1
        which-key-max-display-columns nil
        which-key-min-display-lines 5)
  ;; embolden local bindings
  (set-face-attribute 'which-key-local-map-description-face nil :weight 'bold)
  (which-key-setup-side-window-bottom)
  (which-key-mode))
#+END_SRC
*** Keybindings
    
**** General

#+BEGIN_SRC emacs-lisp
(use-package general
  :config
  (general-evil-setup t)
  (general-auto-unbind-keys)

  (general-create-definer
    +leader
    :prefix "SPC"
    :non-normal-prefix "M-m"
    :states '(motion insert emacs))

  (general-create-definer
    +enable-leader
    :status '(motion normal visual)
    "SPC" nil)

  (general-create-definer
    +local-leader
    :prefix "'"
    :non-normal-prefix "M-m m"
    :states '(motion insert emacs))

  (mmap "M-s" #'evil-write))
#+END_SRC
**** Truely ESC
Truely ESC in minibuffers

#+BEGIN_SRC emacs-lisp
(general-define-key
 :keymaps '(minibuffer-local-map
            minibuffer-local-ns-map
            minibuffer-local-completion-map
            minibuffer-local-must-match-map
            minibuffer-local-isearch-map
            read-expression-map)
 [escape] 'abort-recursive-edit)
#+END_SRC
**** Evil
Install, automatically load, and enable evil. It's like vim, but better!

#+BEGIN_SRC emacs-lisp
(use-package evil
:init
(setq evil-mode-line-format nil)
(setq evil-respect-visual-line-mode t)
(setq evil-want-Y-yank-to-eol t)
(setq evil-symbol-word-search t)
(setq evil-want-C-u-scroll t)
(setq evil-want-visual-char-semi-exclusive t)
(setq evil-ex-search-highlight-all nil)
(evil-mode)
:config
(evil-select-search-module 'evil-search-module 'evil-search)
(mapc #'evil-declare-ignore-repeat
      '(kill-this-buffer
        ido-kill-buffer
        outline-next-visible-heading
        outline-previous-visible-heading
        outline-up-heading
        evil-visualstar/begin-search-forward
        evil-visualstar/begin-search-backward
        org-export-dispatch
        org-end-of-line
        org-beginning-of-line
        org-open-at-point
        org-cycle
        org-shifttab
        org-ctrl-c-ctrl-c
        org-next-visible-heading
        org-previous-visible-heading
        split-window-below
        split-window-below-and-focus
        split-window-right
        split-window-right-and-focus
        evilmi-jump-items))
(defalias 'evil-visual-update-x-selection 'ignore)

(setq evil-normal-state-cursor '(box "DarkGoldenrod2")
      evil-insert-state-cursor '((bar . 2) "chartreuse3")
      evil-emacs-state-cursor '(box "SkyBlue2")
      evil-replace-state-cursor '((hbar . 2) "chocolate")
      evil-visual-state-cursor '((hbar . 2) "gray")
      evil-motion-state-cursor '(box "plum3"))

(setq-default evil-shift-width 2)

(evil-define-motion evil-goto-line (count)
  "Go to the first non-blank character of line COUNT.
 default the (truly) last line."
  :jump t
  :type line
  (if (null count)
      (goto-char (buffer-size))
    (goto-char (point-min))
    (forward-line (1- count)))
  (evil-first-non-blank))

(defun +evil/reselect-paste ()
  "Go back into visual mode and reselect the last pasted region."
  (interactive)
  (cl-destructuring-bind (_ _ _ beg end &optional _)
      evil-last-paste
    (evil-visual-make-selection
     (save-excursion (goto-char beg) (point-marker))
     end)))
:general
(nmap
  "gp" #'+evil/reselect-paste))
#+END_SRC

*** Ivy
Generic completion frontend that's just awesome! Let's install and enable it.

#+BEGIN_SRC emacs-lisp
(use-package ivy
  :demand t)
#+END_SRC

*** Counsel
Counsel allows us to utilize ivy by replacing many built-in and common functions
with richer versions. Let's install it!

#+BEGIN_SRC emacs-lisp
(use-package counsel-projectile)
(use-package counsel
  :demand t)
#+END_SRC

*** Swiper
Swiper is an awesome searching utility with a quick preview. Let's install it and
load it when =swiper= or =swiper-all= is called.

#+BEGIN_SRC emacs-lisp
(use-package swiper
  :commands (swiper swiper-all))
#+END_SRC

*** Magit
The magical git client. Let's load magit only when one of the several entry pont
functions we invoke regularly outside of magit is called.

#+BEGIN_SRC emacs-lisp
(use-package magit
  :init
  ;; avoid max-specpdl-size error,
  ;; See also [[id:CF8198D2-285C-4E0C-9548-2EBBD13D5F50][diff-hl + Magit == 'max-lisp-eval-depth' 'lisp nesting exceeds max-lisp-eval-depth {Mac OS X} · Issue #65 · dgutov/diff-hl]]
  (magit-auto-revert-mode -1)
  :config
  (setq magit-revision-show-gravatars '("^Author:     " . "^Commit:     "))
  :general
  (+enable-leader
   :keymaps
   '(magit-mode-map
     magit-status-mode-map
     magit-diff-mode-map
     magit-process-mode-map
     magit-blame-read-only-mode-map
     magit-log-mode-map))
  (+leader
    "gb" #'magit-blame
    "gs" #'magit-status
    "gi" #'magit-init
    "gl" #'magit-log-buffer-file))
#+END_SRC

**** Evil-Magit

#+BEGIN_SRC emacs-lisp
(use-package evil-magit
  :after magit)
#+END_SRC

*** Projectile
Projectile is a quick and easy project management package that "just works". We're
going to install it and make sure it's loaded immediately.

#+BEGIN_SRC emacs-lisp
(use-package projectile
  :init
  (setq projectile-sort-order 'recentf
        projectile-cache-file (concat +local-directory "projectile.cache")
        projectile-known-projects-file (concat +local-directory "projectile-bookmarks.eld")
        projectile-completion-system 'ivy)
  :config
  (projectile-mode +1)
  :general
  (+leader
    "pa" 'projectile-toggle-between-implementation-and-test
    "pb" 'projectile-switch-to-buffer
    "pd" 'projectile-find-dir
    "pf" 'projectile-find-file
    "pt" 'projectile-find-tag
    "pT" 'projectile-regenerate-tags
    "pI" 'projectile-invalidate-cache
    "pk" 'projectile-kill-buffers
    "pp" 'projectile-switch-project))
#+END_SRC

**** projectile-councel
     
#+BEGIN_SRC emacs-lisp
(use-package counsel-projectile
  :general
  (general-define-key
   [remap projectile-find-file]        #'counsel-projectile-find-file
   [remap projectile-find-dir]         #'counsel-projectile-find-dir
   [remap projectile-switch-to-buffer] #'counsel-projectile-switch-to-buffer
   [remap projectile-grep]             #'counsel-projectile-grep
   [remap projectile-ag]               #'counsel-projectile-ag
   [remap projectile-switch-project]   #'counsel-projectile-switch-project)
  (+leader
   "/" #'counsel-projectile-rg))
#+END_SRC

*** Languages
**** Org
Let's include a newer version of org-mode than the one that is built in. We're going
to manually remove the org directories from the load path, to ensure the version we
want is prioritized instead.

#+BEGIN_SRC emacs-lisp
(use-package org
  :ensure org-plus-contrib
  :pin org
  :defer t)

;; Ensure ELPA org is prioritized above built-in org.
(require 'cl)
(setq load-path (remove-if (lambda (x) (string-match-p "org$" x)) load-path))

(setq org-structure-template-alist
      '(("l" "#+BEGIN_SRC emacs-lisp
?
#+END_SRC")
        ("r" "#+BEGIN_SRC ruby
?
#+END_SRC")
        ("x" "#+BEGIN_SRC elixir
?
#+END_SRC")
        ("s" "#+BEGIN_SRC ?

#+END_SRC")
        ("e" "#+BEGIN_EXAMPLE
?
#+END_EXAMPLE")
        ("q" "#+BEGIN_QUOTE
?
#+END_QUOTE")
        ("c" "#+BEGIN_CENTER
?
#+END_CENTER")))
#+END_SRC

*** Toc-org
Let's install and load the =toc-org= package after org mode is loaded. This is the
package that automatically generates an up to date table of contents for us.

#+BEGIN_SRC emacs-lisp
(use-package toc-org
  :after org
  :init (add-hook 'org-mode-hook #'toc-org-enable))
#+END_SRC

** Post Initialization
Let's lower our GC thresholds back down to a sane level.

#+BEGIN_SRC emacs-lisp
(setq gc-cons-threshold 16777216
      gc-cons-percentage 0.1)
#+END_SRC
